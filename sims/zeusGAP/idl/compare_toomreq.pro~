function hdf, n, name

fileid = hdf_sd_start(name,/read)
sds = hdf_sd_select(fileid,n)
hdf_sd_getdata,sds,data

return, data

end


function aziavg, data3d, nzslice
  nr = n_elements(data3d(*,0,0))
  data1d = dblarr(nr)

  
  for i = 0, nr - 1 do begin
     data1d(i) = mean(data3d(i,nzslice,*))
  endfor
return, data1d
end

pro compare_toomreq, cases=cases, start=start, zslice=zslice $
             ,xtickinterval=xtickinterval $
             ,xrange=xrange, yrange=yrange, ytickinterval=ytickinterval $
             , mp=mp $
             ,legend=legend, label=label

;get the basic info

location =strcompress(cases(0),/remove_all)	
filename = strcompress('hdfaa.000',/remove_all)
fileloc  = filepath(filename,root_dir='.',subdir=[location])

rad  = hdf(2, fileloc)
theta= hdf(1, fileloc)
phi  = hdf(0, fileloc)

nrad  = n_elements(rad)
ntheta= n_elements(theta)
nphi  = n_elements(phi)

data2d = dblarr(nrad, nphi)

vphi1d = dblarr(nrad)
den1d  = dblarr(nrad)

omega_k = rad^(-3./2.)
;plot the first case

ks   = string(start(0),format='(I03)')
filename = strcompress('hdfaa.'+ks,/remove_all)
fileloc  = filepath(filename,root_dir='.',subdir=[location])

vphi  = hdf(11, fileloc)
den   = hdf(19, fileloc)


nzslice = fix(zslice*ntheta)
if(nzslice eq ntheta) then nzslice -= 1

vphi1d = aziavg(vphi, nzslice)
den1d  = aziavg(den, nzslice)


ksq = deriv(rad, rad^2.*vphi1d^2.)/rad^3.
kappa = sqrt(ksq)

toomreQ = kappa*omega_k/(2.0*!dpi*den1d) ;approx sigma = 2*rho*H

if keyword_set(mp) then begin
                                ; planet info
   nlines = file_lines(filepath('planetxy_hdf.dat',root_dir='.',subdir=[location]))
   planetinfo = dblarr(7,nlines)
   openr, 1, filepath('planetxy_hdf.dat',root_dir='.',subdir=[location])
   readf,1,planetinfo
   close,1
   
   plx=planetinfo(1,start(0))
   ply=planetinfo(2,start(0))
   plz=planetinfo(3,start(0))
   plrad = sqrt(plx^2 + ply^2 + plz^2)
   rhill = plrad*(mp(0)/3d0)^(1d0/3d0)
   rplot = (rad - plrad)/rhill
   xtitle = textoidl('(r - r_p)/r_h')
endif else begin
   rplot = rad
   xtitle = textoidl('r')
endelse


set_plot, 'ps'
device, filename=strcompress('compare_toomreq_'+string(start(0),format='(I03)')+'.ps',/remove_all) $
        ,bits_per_pixel=8,xsize=8, ysize=4.5,xoffset=0,yoffset=0,/inches

plot, rplot, toomreq, xmargin=[8,1],ymargin=[3.5,0.5], ystyle=0  $
      ,charsize=1.5, thick=4, xrange=xrange, yrange=yrange, xtitle=xtitle, ytickinterval=ytickinterval  $
      ,xtickinterval=xtickinterval, ytitle='Q', linestyle=0

;overplot the other cases

for k=1, n_elements(cases) - 1 do begin
   
   location =strcompress(cases(k),/remove_all)
   
   ks   = string(start(k),format='(I03)')
   filename = strcompress('hdfaa.'+ks,/remove_all)
   fileloc  = filepath(filename,root_dir='.',subdir=[location])

        
   vphi  = hdf(11, fileloc)
   den   = hdf(19, fileloc)


   nzslice = fix(zslice*ntheta)
   vphi1d = aziavg(vphi, nzslice)
   den1d  = aziavg(den, nzslice)

   ksq = deriv(rad, rad^2.*vphi1d^2.)/rad^3.
   kappa = sqrt(ksq)

   toomreQ = kappa*omega_k/(2.0*!dpi*den1d) ;approx sigma = 2*rho*H

   if keyword_set(mp) then begin
                                ; planet info
      nlines = file_lines(filepath('planetxy_hdf.dat',root_dir='.',subdir=[location]))
      planetinfo = dblarr(7,nlines)
      openr, 1, filepath('planetxy_hdf.dat',root_dir='.',subdir=[location])
      readf,1,planetinfo
      close,1
      
      plx=planetinfo(1,start(k))
      ply=planetinfo(2,start(k))
      plz=planetinfo(3,start(k))
      plrad = sqrt(plx^2 + ply^2 + plz^2)
      rhill = plrad*(mp(k)/3d0)^(1d0/3d0)
      rplot = (rad - plrad)/rhill
   endif else begin
      rplot = rad
   endelse
   
   oplot, rplot, toomreq, thick=4, linestyle=k 
   
endfor


   if keyword_set(legend) then begin
   x0=legend(0)
   x1=legend(1)
   y0=legend(2)
   dy=legend(3)
	for j=0, n_elements(label)-1 do begin
    oplot, [x0,x1], [y0,y0]-dy*j, thick=4, linestyle=j
    xyouts, x1, y0-dy*j,textoidl(label(j)),charsize=1.5
    endfor
    endif

 device,/close

end
