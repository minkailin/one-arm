function hdf, n, name

fileid = hdf_sd_start(name,/read)
sds = hdf_sd_select(fileid,n)
hdf_sd_getdata,sds,data

return, data
end

function bigH, bigR, rref, hsmall, qsmall

height = hsmall*rref*(rref/bigR)^(0.5d0*(qsmall-3d0))

return, height
end


pro polar_xy, loc=loc, start=start, finish=finish, zslice=zslice $
           ,vertavg=vertavg, type=type, log=log, plotrange0=plotrange0 $
           ,xtickinterval=xtickinterval, ytickinterval=ytickinterval, nopert=nopert, tuniv=tuniv $
           ,hole=hole, aziline=aziline, basic=basic, xrange=xrange, yrange=yrange, mp=mp $
           ,name=name, smallq=smallq, smallh=smallh, ct=ct, nonaxi=nonaxi, phi0=phi0

  if not keyword_set(finish) then finish = start
  if not keyword_set(ct) then ct =5 
  loadct,ct,/silent

;get the basic info
  location =strcompress(loc,/remove_all)

  if not keyword_set(basic) then begin
     filename = strcompress('hdfaa.000',/remove_all)
  endif else begin
     nme = string(basic,format='(I03)')
     filename = strcompress('hdfaa.'+nme,/remove_all)
  endelse
  if keyword_set(nonaxi) then filename = strcompress('hdfaa.'+string(start,format='(I03)'),/remove_all)

  fileloc  = filepath(filename,root_dir='.',subdir=[location])
  
  rad  = hdf(2, fileloc)
  theta= hdf(1, fileloc)
  phi  = hdf(0, fileloc)
  
  nrad  = n_elements(rad)
  ntheta= n_elements(theta)
  nphi  = n_elements(phi)
  
; assume uniform theta and phi spacing
  
  dtheta = theta(1) - theta(0)
  dphi = phi(1) - phi(0)
  
  data_axisymmetric = dblarr(nrad, ntheta)
  data2d = dblarr(nrad, nphi)
  dataplot=dblarr(nrad, nphi)
  
  case type of
     'vrad':   data0   = hdf(3, fileloc)
     'vtheta': data0   = hdf(7, fileloc)
     'vphi':   data0   = hdf(11, fileloc)
     'pot':    data0   = hdf(15, fileloc)
     'dens':   data0   = hdf(19, fileloc)
     'en':   data0   = hdf(23, fileloc)
  endcase
  
  for j=0, ntheta-1 do begin
     for i=0, nrad-1 do begin
        data_axisymmetric(i,j) = mean(data0(i,j,*))
     endfor
  endfor
  for k=0, nphi-1 do begin
     data0(*,*,k) = data_axisymmetric(*,*)
  endfor
 
; planet info
  nlines = file_lines(filepath('planetxy_hdf.dat',root_dir='.',subdir=[location]))
  planetinfo = dblarr(7,nlines)
  openr, 1, filepath('planetxy_hdf.dat',root_dir='.',subdir=[location])
  readf,1,planetinfo
  close,1
  torb = 2d0*!dpi*planetinfo(1,0)^(3d0/2d0)
  
  nzslice = fix(zslice*ntheta)
  height  = 1d0/(smallh*tan(theta(nzslice)))
  heightstring = string(height,format='(F4.1)')

  
  
  for n=start, finish do begin
     ks   = string(n,format='(I03)')
     filename = strcompress('hdfaa.'+ks,/remove_all)
     fileloc  = filepath(filename,root_dir='.',subdir=[location])
     
     case type of
        'vrad':   data   = hdf(3, fileloc)
        'vtheta': data   = hdf(7, fileloc)
        'vphi':   data   = hdf(11, fileloc)
        'pot':    data   = hdf(15, fileloc)
        'dens':   data   = hdf(19, fileloc)
        'en':   data   = hdf(23, fileloc)
     endcase

     plx=planetinfo(1,n)
     ply=planetinfo(2,n)
     plz=planetinfo(3,n)
     phiplt = pltphi(plx,ply)
     plrad = sqrt(plx^2 + ply^2 + plz^2)
     

     if keyword_set(mp) then begin
        rhill = plrad*(mp/3d0)^(1d0/3d0)
        rplot = (rad - plrad)/rhill
        xtitle=textoidl('(r-r_p)/r_h')
     endif else begin
        rplot = rad/plrad
        xtitle=textoidl('r/r_0')
     endelse
     
     if not keyword_set(phi0) then begin
        phiplt = pltphi(plx,ply)
     endif else begin
        phiplt = phi0*2d0*!dpi
     endelse
     
     zmax = plrad*cos(theta(nzslice)) ;height at rp
     zmax/=  bigH(plrad, plrad, smallh, smallq)
     print, 'zmax/H at plrad =', zmax
     
     if not keyword_set(nopert) then begin
        if not keyword_set(log) then data = data - data0
        data/=data0
     endif
     
     data2d(0:nrad-1,0:nphi-1) = data(0:nrad-1,nzslice,0:nphi-1)
    
    if keyword_set(log) then data2d=alog10(data2d)

    if not keyword_set(plotrange0) then begin
       temp = min(abs(rplot - xrange(0)),r1)
       temp = min(abs(rplot - xrange(1)),r2)
       plotrange=[min(data2d(r1:r2,*)),max(data2d(r1:r2,*))]
    endif else plotrange=plotrange0
    
    levels=plotrange(0)+(plotrange(1)-plotrange(0))*(dindgen(48)/47.)
;    time=string(planetinfo(0,n)/torb,format='(F6.1)')
    time=string(planetinfo(0,n)/torb,format='(F4.1)')   
    if(phiplt gt !dpi) then begin
       temp2=min(abs(phi-(phiplt-!dpi)),grid2)
       dataplot(0:nrad-1,0:nphi-1-grid2) = data2d(0:nrad-1,grid2:nphi-1)
       dataplot(0:nrad-1,nphi-grid2:nphi-1) = data2d(0:nrad-1, 0:grid2-1)
    endif
    if(phiplt lt !dpi) then begin
       temp2=min(abs(phi-(phiplt+!dpi)),grid2)
       dataplot(0:nrad-1,nphi-grid2:nphi-1) = data2d(0:nrad-1,0:grid2-1)
       dataplot(0:nrad-1,0:nphi-1-grid2) = data2d(0:nrad-1, grid2:nphi-1)
    endif    
    if(phiplt eq !dpi) then dataplot = data2d
    
    title  = time+textoidl('P_0')
    ytitle = textoidl('(\phi-\phi_0)/\pi')
    if keyword_set(mp) then begin
       ;title += textoidl(', r_p='+string(plrad,format='(f5.2)'))
       ytitle = textoidl('(\phi-\phi_p)/\pi')
    endif

    
    title = title+strcompress(textoidl(',(h.tan\theta)^{-1}='+heightstring),/remove_all)

    set_plot, 'ps'
    device, filename=filepath(strcompress('polarxy_'+type+ks+'.ps',/remove_all),root_dir='.',subdir=[location])$
            ,/color, bits_per_pixel=8,xsize=12, ysize=14
    contour,dataplot,rplot,phi/!dpi - 1.0 ,/fill,levels=levels,title=title, xstyle=1, $
            xtitle=xtitle, ytitle=ytitle,charsize=1.5,xmargin=[7.0,6.0], ymargin=[3.5,2], $
            xtickinterval=xtickinterval,xrange=xrange, yrange=yrange, xminor=5, ytickinterval=ytickinterval
    
;    xyouts, -2, -.98, 'ZEUS 3D', charsize=3.5,charthick=8, color=255 
    colorbar, position=[0.865, 0.15, 0.90, 0.90],/vertical,/right,range=plotrange,format='(f5.2)'
    oplot,[0,0],[0.0,0.0]/!dpi,psym=7,symsize=1.5,color=!D.Table_size*1.1
    if keyword_set(aziline) then begin
       for j=0, n_elements(aziline)-1 do begin
          oplot,[-1,1]*1d2,[aziline(j),aziline(j)], color=!D.Table_size, thick=4, linestyle=1
          print, 'azislice=', (aziline(j)*!dpi + phiplt)/(2d0*!dpi)
       endfor
    endif  
    
    if keyword_set(name) then begin
       xyouts, xrange(1), -0.95, textoidl(name),charsize=2, charthick=6, color=255, align=1
    endif
    
    
    
    device,/close
    print, 'done '+ks
    
 endfor
end
